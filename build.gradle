buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = "${minecraft_version}-${mod_version}"
group = "noobanidus.mods.overworldrespawn"
archivesBaseName = "uncrafting-blacklist"

sourceCompatibility = targetCompatibility = "1.8"

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"
    mappings = "${mcp_mappings}"
    useDepAts = true

    def args = [
            "-Dfml.coreMods.load=noobanidus.mods.overworldrespawn.core.OWLoadingPlugin",
            "-Dmixin.hotSwap=true",
            "-Dmixin.checks.interfaces=true"
    ]
    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)

    replace "GRADLE:VERSION", project.version
}

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        url "https://maven.mcmoddev.com/"
    }
    maven {
        name = "sponge"
        url = "http://repo.spongepowered.org/maven/"
    }
}

dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'

    shade("org.spongepowered:mixin:0.8") {
        transitive = false
        exclude module: "asm-commons"
        exclude module: "asm-tree"
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "log4j-core"
        exclude module: "gson"
        exclude module: "commons-io"
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        expand "version": project.version, "mc_version": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }

    rename '(.+at\\.cfg)', 'META-INF/$1'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = "deobf"
}

jar {
    manifest {
        attributes([
                "FMLCorePluginContainsFMLMod": "true",
                "FMLCorePlugin"              : "noobanidus.mods.overworldrespawn.core.OWLoadingPlugin",
                "TweakClass"                 : "org.spongepowered.asm.launch.MixinTweaker",
                "ForceLoadAsMod"             : "true",
                "FMLAT"                      : "ow_at.cfg"
        ])
    }

    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            rename 'LICENSE.txt', 'org/spongepowered/LICENSE.txt'
            exclude '**/*.html'
        }
    }
}

artifacts {
    archives deobfJar
}

mixin {
    add sourceSets.main, "mixins.overworldrespawn.refmap.json"
}
